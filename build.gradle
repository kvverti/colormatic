plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
}

repositories {
	maven { url = 'https://maven.terraformersmc.com/releases/' }
	maven { url = 'https://maven.shedaniel.me/' }
	maven { url = 'https://api.modrinth.com/maven' }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc.${project.minecraft_version}"
group = project.maven_group

loom {
	splitEnvironmentSourceSets()

	mods {
		"colormatic" {
			// we're a client-only mod
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
	modCompileOnly fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	modCompileOnly("com.terraformersmc:modmenu:${project.mod_menu_version}") {
		transitive = false
	}
	modCompileOnly "maven.modrinth:sodium:${project.sodium_version}"

	// runtime only setups
	 modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	 modRuntimeOnly("com.terraformersmc:modmenu:${project.mod_menu_version}") { transitive = false }
	// modRuntimeOnly fabricApi.module("fabric-rendering-data-attachment-v1", project.fabric_version)
	// modRuntimeOnly fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)
	 modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version}"

	// because Loom doesn't load Sodium's included non-mod dependency
	 runtimeOnly "org.joml:joml:1.10.2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
	it.options.encoding = "UTF-8"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from "LICENSE"
	from "COPYING.LESSER"
	from "COPYING.ADDITIONAL"
}
